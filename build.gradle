plugins {
    id 'idea'
    id 'java'
    id 'io.freefair.lombok' version '8.1.0'
    id 'maven-publish'
}

group = 'com.github.rmarinsky'
version = '0.9'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
    withSourcesJar()
    withJavadocJar()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

jar {
    archiveBaseName = project.name
    version = project.version
}

dependencies {
    implementation(
            'com.microsoft.playwright:playwright:1.40.0',
            'org.junit.jupiter:junit-jupiter'
    )
    implementation platform('org.junit:junit-bom:5.9.1')
}

test {
    useJUnitPlatform()
}

task pwServeTraces(type: JavaExec, group: 'pw') {
    mainClass = 'com.microsoft.playwright.CLI'
    classpath = sourceSets.main.runtimeClasspath
    args 'show-trace', 'build/pw/' + System.getProperty("trace") + '.zip'
    systemProperties = System.getProperties()
}


publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = project.version

            from components.java

            pom {
                description = 'Drama - wrapper for Playwright inspired by Selenide'
            }
        }
    }
    repositories {
        mavenLocal()
    }
}